apply plugin: 'com.android.application'
apply plugin: 'findbugs'

android {
    compileSdkVersion 23
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "protect.rentalcalc"
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 1
        versionName "0.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

// On Travis-CI the unit tests consume too much memory. This may be
// due to a leak when running Robolectric tests. To isolate the
// impact of potential leaks, each test is run in its own process.
android.testOptions.unitTests.all {
    maxParallelForks = 1
    forkEvery = 1
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile group: 'com.google.guava', name: 'guava', version: '20.0'
    compile 'io.github.kexanie.library:MathView:0.0.6'
    testCompile 'junit:junit:4.12'
    testCompile "org.robolectric:robolectric:3.3.1"
    // To get around issue with robolectric not finding
    // javax/microedition/khronos/opengles/GL
    // https://github.com/robolectric/robolectric/issues/1932
    testCompile 'org.khronos:opengl-api:gl1.1-android-2.1_r1'
}

findbugs {
    sourceSets = []
    ignoreFailures = false
}

task findbugs(type: FindBugs, dependsOn: 'assembleDebug') {

    description 'Run findbugs'
    group 'verification'

    classes = fileTree('build/intermediates/classes/debug/')
    source = fileTree('src/main/java')
    classpath = files()

    effort = 'max'

    excludeFilter = file("./config/findbugs/exclude.xml")

    reports {
        xml.enabled = false
        html.enabled = true
    }
}
